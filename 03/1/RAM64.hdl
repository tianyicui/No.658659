// This file is part of the materials accompanying the book 
// "The Elements of Computing Systems" by Nisan and Schocken, 
// MIT Press. Book site: www.idc.ac.il/tecs
// File name: projects/03/1/RAM64.hdl

/**
 * Memory of 64 registers, each 16-bit wide.  
 * The chip facilitates read and write operations, as follows:
 *     Read:  out(t) = RAM64[address(t)](t)
 *     Write: If load(t-1) then RAM64[address(t-1)](t) = in(t-1)
 * In words: the chip always outputs the value stored at the memory 
 * location specified by address. If load=1, the in value is loaded 
 * into the memory location specified by address.  This value becomes 
 * available through the out output starting from the next time step.
 */

CHIP RAM64 {

    IN in[16], load, address[6];
    OUT out[16];

    PARTS:
    RAM8(address=address[0..2], load=l0, in=in, out=out0);
    RAM8(address=address[0..2], load=l1, in=in, out=out1);
    RAM8(address=address[0..2], load=l2, in=in, out=out2);
    RAM8(address=address[0..2], load=l3, in=in, out=out3);
    RAM8(address=address[0..2], load=l4, in=in, out=out4);
    RAM8(address=address[0..2], load=l5, in=in, out=out5);
    RAM8(address=address[0..2], load=l6, in=in, out=out6);
    RAM8(address=address[0..2], load=l7, in=in, out=out7);

    DMux8Way(sel=address[3..5], in=load, a=l0, b=l1, c=l2, d=l3, e=l4, f=l5, g=l6, h=l7);
    Mux8Way16(sel=address[3..5], a=out0, b=out1, c=out2, d=out3, e=out4, f=out5, g=out6, h=out7, out=out);

}
